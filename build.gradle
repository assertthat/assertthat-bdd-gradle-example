
buildscript {
    dependencies{
        classpath group: 'com.assertthat.plugins', name: 'assertthat-bdd-gradle-tasks', version: '1.6'
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

dependencies {
    testCompile group: 'io.cucumber', name: 'cucumber-java', version: '4.2.0'
    testCompile group: 'io.cucumber', name: 'cucumber-junit', version: '4.2.0'
}

task downloadFeatures(type: FeaturesTask){
    /*Jira project id e.g. 10001*/
    projectId ="PROJECT_ID"
    /*Optional - can be supplied as environment variable ASSERTTHAT_ACCESS_KEY*/
    accessKey = "ASSERTTHAT_ACCESS_KEY"
    /*Optional - can be supplied as environment variable ASSERTTHAT_SECRET_KEY*/
    secretKey = "ASSERTTHAT_SECRET_KEY"
    /*Optional - default ./features*/
    outputFolder = "src/test/resources"
    /*Optional - all features downloaded by default - should be a valid JQL*/
    jql = "project = XX AND key in ('XXX-1')"
    /*Optional - default automated (can be one of: manual/automated/both)*/
    mode = "automated"
    /*Optional - the value MUST be an instance of {@link String} or {@link java.net.URI}.*/
    proxyURI = "myproxy:8080"
    /*Optional - user name which will be used for proxy authentication.*/
    proxyUsername = "username"
    /*Optional - password which will be used for proxy authentication.*/
    proxyPassword = "password"
}

task cucumber() {
    dependsOn assemble, compileTestJava, downloadFeatures
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'json:reports/cucumber.json', '--glue', 'gradle.cucumber', 'src/test/resources']
        }
    }
}

task submitReport(type: ReportTask){
    /*Jira project id e.g. 10001*/
    projectId ="PROJECT_ID"
    /*Optional - can be supplied as environment variable ASSERTTHAT_ACCESS_KEY*/
    accessKey = "ASSERTTHAT_ACCESS_KEY"
    /*Optional - can be supplied as environment variable ASSERTTHAT_SECRET_KEY*/
    secretKey = "ASSERTTHAT_SECRET_KEY"
    /*Optional - the name of the run - default 'Test run dd MMM yyyy HH:mm:ss'*/
    runName = "Dry Tests Run"
    /*Optional - json report folder - default ./reports*/
    jsonReportFolder = "reports"
    /*Optional - regex to search for cucumber reports - default **.json*/
    jsonReportIncludePattern = "**/cucumber.json"
    /*Optional - the value MUST be an instance of {@link String} or {@link java.net.URI}.*/
    proxyURI = "myproxy:8080"
    /*Optional - user name which will be used for proxy authentication.*/
    proxyUsername = "username"
    /*Optional - password which will be used for proxy authentication.*/
    proxyPassword = "password"
}

repositories {
    jcenter()
}

cucumber.finalizedBy submitReport
